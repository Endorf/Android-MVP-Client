// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '7.4.2' apply false
    id 'com.android.library' version '7.4.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.10' apply false
    id 'io.gitlab.arturbosch.detekt' version '1.22.0'
    id 'org.jlleitschuh.gradle.ktlint' version '11.4.0'
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "io.gitlab.arturbosch.detekt"

    afterEvaluate {
        if(it.hasProperty('android')){
            android {
                lintOptions {
                    checkDependencies true
                    showAll true
                    explainIssues false
                    lintConfig file("$rootDir/.lint/lint.xml")
                    baseline file("$rootDir/.lint/lint-baseline.xml")
                }
            }
        }
    }

    ktlint {
        android.set(true)
        outputColorName.set("RED")
        outputToConsole.set(true)
        disabledRules = ["final-newline", "import-ordering"]
    }

    detekt {
        buildUponDefaultConfig = true
        parallel = true
        config = files("$project.rootDir/.detekt/config.yml")
        baseline = file("$project.rootDir/.detekt/detekt-baseline.xml")
    }
}

apply from: 'gradle/util.gradle'

task clean(type: Delete) {
    delete rootProject.buildDir
}